/*
 * Copyright 2021 German Vekhorev (DarksideCode)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
}

group 'me.darksidecode.jminima'
version '1.1.1'

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url 'https://archiva.reflex.rip/repository/public/'

            credentials {
                // ~/.gradle/gradle.properties
                username reflexPrivateUsername
                password reflexPrivatePassword
            }

            authentication {
                basic BasicAuthentication
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'jminima'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'JMinima'
                description = 'Minimalistic declarative JVM instrumentation library on top of ASM'
                url = 'https://github.com/MeGysssTaa/jminima'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'DarksideCode'
                        name = 'German Vekhorev'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()

    maven {
        name 'Public Reflex Repository'
        url 'https://archiva.reflex.rip/repository/public/'
    }
}

def asmVersion = '9.1'
def lombokVersion = '1.18.20'

dependencies {
    compileOnly group: 'commons-io', name: 'commons-io', version: '2.8.0'
    compileOnly group: 'org.ow2.asm', name: 'asm', version: "${asmVersion}"
    compileOnly group: 'org.ow2.asm', name: 'asm-util', version: "${asmVersion}"
    compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
}
